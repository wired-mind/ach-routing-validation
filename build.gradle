plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
    id 'idea'
}

group 'com.lyricfinancial.routingvalidation'

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException('''A Haiku:
                                      |  This needs Java 8,
                                      |  You are using something else,
                                      |  Refresh. Try again.'''.stripMargin())
}

repositories {
    jcenter()
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    flatDir {
        dirs 'lib'
    }
}

sourceCompatibility = '1.8'
mainClassName = 'io.vertx.core.Launcher'

sourceSets {
    generated{
        java.srcDir "${projectDir}/src/main/generated"
    }
}

dependencies {
    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-service-factory:$vertxVersion"
    compile "io.vertx:vertx-service-proxy:$vertxVersion"
    compile "io.vertx:vertx-codegen:$vertxVersion"
    compile "io.vertx:vertx-web:$vertxVersion"
    compile "io.vertx:vertx-sql-common:$vertxVersion"
    compile "io.vertx:vertx-mysql-postgresql-client:$vertxVersion"
    compile name: "jaunt1.1.3"
    testCompile "io.vertx:vertx-unit:$vertxVersion"
    testCompile "junit:junit:$junitVersion"
}

task generateProxies(type: JavaCompile, group: 'build',
        description: 'Generates the Vertx proxies') {
    source = sourceSets.main.java
    classpath = configurations.compile
    options.compilerArgs = [
            "-proc:only",
            "-processor", "io.vertx.codegen.CodeGenProcessor",
            "-AoutputDirectory=${projectDir}/src/main"
    ]
    destinationDir = file("${projectDir}/src/main/generated")
}

compileJava{
    dependsOn(generateProxies)
    source    += sourceSets.generated.java
    options.compilerArgs = [
            "-Acodetrans.output=${projectDir}/src/main"
    ]
}

clean {
    delete += sourceSets.generated.java.srcDirs
}


shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Launcher'
        attributes 'Main-Verticle': 'com.lyricfinancial.routingvalidation.Start'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}
